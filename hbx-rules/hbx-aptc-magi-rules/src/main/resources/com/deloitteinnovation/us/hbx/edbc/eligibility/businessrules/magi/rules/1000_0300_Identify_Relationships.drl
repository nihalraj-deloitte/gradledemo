package com.deloitteinnovation.us.hbx.edbc.eligibility.businessrules.magi.rules;
import com.deloitteinnovation.us.hbx.edbc.eligibility.businessrules.magi.Case;
import com.deloitteinnovation.us.hbx.edbc.eligibility.businessrules.magi.Individual;
import com.deloitteinnovation.us.hbx.edbc.eligibility.businessrules.magi.RelationShip;
import com.deloitteinnovation.us.hbx.edbc.eligibility.businessrules.magi.constants.RelationshipType;
import java.util.Arrays;
import java.lang.String;

rule "Rule_1000_0300_Identify_Relationships_1"
	salience 65535
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual($indiv1RelationShip:relationships) from $caseIndividual
		$indiv2:Individual() from $caseIndividual
		$rel:RelationShip(
			RelationshipCode in (
				RelationshipType.BROTHEROR_HALF_BROTHER.value,
				RelationshipType.SISTEROR_HALF_SISTER.value,
				RelationshipType.STEP_SISTER.value,
				RelationshipType.STEP_BROTHER.value
			) 
			&& RefIndividualId == $indiv2.IndividualId
		)from $indiv1RelationShip
	then
		$indiv1.getsiblings().add($indiv2);
		$indiv1.setIsRelatedToOtherIndividual(true);
end

rule "Rule_1000_0300_Identify_Relationships_2"
	salience 65534
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual($indiv1RelationShip:relationships) from $caseIndividual
		$indiv2:Individual() from $caseIndividual
		$rel:RelationShip(
			RelationshipCode in (
				RelationshipType.MOTHER.value,
				RelationshipType.FATHER.value, 
				RelationshipType.STEPMOTHER.value, 
				RelationshipType.STEPFATHER.value
			) 
			&& RefIndividualId == $indiv2.IndividualId
		) from $indiv1RelationShip
	then
		$indiv1.getchildren().add($indiv2);
		$indiv1.setIsRelatedToOtherIndividual(true);
end

rule "Rule_1000_0300_Identify_Relationships_3"
	salience 65533
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual($indiv1RelationShip:relationships) from $caseIndividual
		$indiv2:Individual() from $caseIndividual
		$rel:RelationShip(
			RelationshipCode in (
				RelationshipType.HUSBAND.value,
				RelationshipType.WIFE.value,
				RelationshipType.SPOUSE.value
			) 
			&& RefIndividualId == $indiv2.IndividualId
		) from $indiv1RelationShip
	then
		$indiv1.getspouse().add($indiv2);
		$indiv1.setIsRelatedToOtherIndividual(true);
end

rule "Rule_1000_0300_Identify_Relationships_4"
	salience 65532
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual($indiv1RelationShip:relationships) from $caseIndividual
		$indiv2:Individual() from $caseIndividual
		$rel:RelationShip(
			RelationshipCode in (
				RelationshipType.SON.value,
				RelationshipType.DAUGHTER.value, 
				RelationshipType.STEPSON.value, 
				RelationshipType.STEPDAUGHTER.value, 
				RelationshipType.ADOPTED_SON.value, 
				RelationshipType.ADOPTED_DAUGHTER.value
			) 
			&& RefIndividualId == $indiv2.IndividualId)
		 from $indiv1RelationShip
	then
		$indiv1.getparents().add($indiv2);
		$indiv1.setIsRelatedToOtherIndividual(true);
end

rule "Rule_1000_0300_Identify_Relationships_5"
	salience 65531
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual($indiv1RelationShip:relationships) from $caseIndividual
		$indiv2:Individual() from $caseIndividual
		$rel:RelationShip(
			RelationshipCode == RelationshipType.LIVING_TOGETHER_PARTNER.value 
			&& RefIndividualId == $indiv2.IndividualId
		) from $indiv1RelationShip
	then
		$indiv1.getlivingTogetherPartner().add($indiv2);
		$indiv1.setIsRelatedToOtherIndividual(true);
end

rule "Rule_1000_0300_Identify_Relationships_6"
	salience 65530
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual() from $caseIndividual
		$indiv2:Individual($indiv2RelationShip:relationships) from $caseIndividual
		$rel2:RelationShip(
			$indiv1.IndividualId == RefIndividualId 
			&& HasFillsParentalRole == true
		) from $indiv2RelationShip
	then
		$indiv1.getvalidCareTaker().add($indiv2);
end

rule "Rule_1000_0300_Identify_Relationships_7"
	salience 65529
	ruleflow-group "1000_0300_Identify_Relationships"
	when
		$case:Case($caseIndividual:individual)
		$indiv1:Individual($indiv1RelationShip:relationships) from $caseIndividual
		$indiv2:Individual() from $caseIndividual
		$rel:RelationShip(
			RelationshipCode in (
				RelationshipType.SON.value,
				RelationshipType.DAUGHTER.value,
				RelationshipType.MOTHER.value,
				RelationshipType.FATHER.value, 
				RelationshipType.BROTHEROR_HALF_BROTHER.value,
				RelationshipType.SISTEROR_HALF_SISTER.value,
				RelationshipType.GRANDSONOR_GREAT_GRANDSON.value, 
				RelationshipType.GRANDDAUGHTEROR_GREAT_GRANDDAUGHTER.value,
				RelationshipType.GRANDFATHEROR_GREAT_GRANDFATHER.value,
				RelationshipType.GRANDMOTHEROR_GREAT_GRANDMOTHER.value,
				RelationshipType.STEPSON.value, 
				RelationshipType.STEPDAUGHTER.value,
				RelationshipType.STEPMOTHER.value,
				RelationshipType.STEPFATHER.value,
				RelationshipType.HUSBAND.value,
				RelationshipType.WIFE.value,
				RelationshipType.AUNT.value,
				RelationshipType.UNCLE.value,
				RelationshipType.NIECE.value,
				RelationshipType.NEPHEW.value,
				RelationshipType.FOSTER_CHILD.value,
				RelationshipType.FOSTER_PARENT.value,
				RelationshipType.POTENTIAL_PUTATIVEFATHER.value, 
				RelationshipType.UNRELATED_PLACEMENT_COURT_ORDERED.value,
				RelationshipType.STEP_SISTER.value, 
				RelationshipType.STEP_BROTHER.value, 
				RelationshipType.FIRST_COUSIN.value, 
				RelationshipType.FIRST_COUSIN_ONCE_REMOVED.value, 
				RelationshipType.LEGALLY_GUARDED.value, 
				RelationshipType.LEGAL_GUARDIAN.value,
				RelationshipType.PENDING_LEGAL_GUARDIAN.value, 
				RelationshipType.LIVING_TOGETHER_PARTNER.value, 
				RelationshipType.STEP_GRANDMOTHERORSTEP_GREAT_GRANDMOTHER.value, 
				RelationshipType.STEP_GRANDFATHERORSTEP_GREAT_GRANDFATHER.value,
				RelationshipType.ADOPTED_SON.value, 
				RelationshipType.ADOPTED_DAUGHTER.value, 
				RelationshipType.STEP_GRANDDAUGHTER.value,
				RelationshipType.STEP_GRANDSON.value
			) 
	    	&& IndividualId == $indiv1.IndividualId
	    ) from $indiv1RelationShip
	then
		$indiv1.setIsRelatedToOtherIndividual(true);
end